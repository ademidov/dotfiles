set nocompatible    " we are in 2013
let mapleader = ' '

" Load plugins
source $HOME/.vim/plugins.vim

runtime macros/matchit.vim

" Section: Options {{{1

  set autoread          " look for external writings to the current buffer
  set history=1000      " loong commands history
  set splitright        " put new windows on the right
  set splitbelow        " and below
  set textwidth=79      " universal width
  set nowrap            " do not wrap lines
  set hidden            " buffers can live in background
  set ttyfast           " send more characters at a given time
  set shellpipe=&>      " prevent ack output to bleed into the terminal
  set clipboard=unnamed " OS X clipboard integration
  " set complete-=i       " comletion from included may be slow

  set nrformats-=octal  " do not use C-A C-X on octals

  set ttimeout
  set ttimeoutlen=10 " fast escape
  set lazyredraw     " don't redraw while macro executing

  set formatoptions+=j " Delete comment character when joining lines

  set diffopt=filler,vertical

  " More faster scrolling: http://vim.wikia.com/wiki/Fix_syntax_highlighting
  syntax sync minlines=256

  " Interface {{{
    " set term=xterm-256color
    " set term=gnome-256color
    set t_Co=256       " a lot of colors in terminal

    set ruler          " show the cursor position all the time
    set nocursorline   " do not HL cursor line
    set nocursorcolumn " does not HL cursor column
    set laststatus=2   " always show status line
    set showcmd        " show the current command at lower right corner
    set report=0       " always report changed lines count
    set shortmess=atI  " strangle annoying prompts

    set scrolloff=8     " 8 lines around cursor vert
    set sidescrolloff=8 " 8 lines around cursor horizontally

    set list                      " show unprintable characters
    set listchars=tab:▸\ ,trail:· " display extra whitespaces with dots and tabs with triangles

    set number           " show line numbers
    set numberwidth=1    " as small as possible width for numbers
    set norelativenumber " relative numbers not so awesome
  "}}}
  " Search and replace {{{
    set hlsearch   " highlight search
    set incsearch  " show search matches while typing
    set ignorecase " case insensitive search
    set smartcase  " case sensitive when capitals present

    set gdefault   " replace all occurences of a line with substitutions (default s//g)
  "}}}
  " Text formatting {{{
    set autoindent                 " preserve indentation
    set backspace=indent,eol,start " make backspace normal for programmer, not for typewriter

    set expandtab                  " use spaces instead tabs
    set shiftwidth=2               " 2 spaces for indenting with >>
    set tabstop=2                  " 2 spaces for tab
    set softtabstop=2              " 2 spaces for tab
    set shiftround
  "}}}
  " Command line completions {{{
    set wildmenu                               " enable enhanced completion
    set wildmode=list:longest,full             " list all matches, complete to longest common and toggle by tab
    set wildignore=git,*.swp,*.jpg,*.png,*.gif " ignore versioning and binary files at autocomplete
  "}}}
  " Status line {{{

    fun! StatusLineFilePath() "{{{
      let p = expand('%')
      if len(p) > 30
        let p = pathshorten(p)
      endif
      return p
    endfunction "}}}

    set statusline=
    set statusline+=\ %<%{StatusLineFilePath()} " path
    set statusline+=%m%r%h%w                    " flags
    set statusline+=%=                          " switch to right side
    set statusline+=%y\                         " type
    set statusline+=%-10((%l,%c)%)\             " line and column
    set statusline+=%3P                         " percentage of file
    " set statusline+=[%{fugitive#head()}]          " git branch
  "}}}
  " Persistent undo {{{
    silent !mkdir $HOME/.vim/undo > /dev/null 2>&1
    set undodir=~/.vim/undo                       " where to store undo files
    set undofile                                  " enable undofile
    set undolevels=500                            " max undos stored
  "}}}
  " Russian language support {{{
    set keymap=russian-jcukenwin
    set iminsert=0               " Use latin by default
    set imsearch=0               " Use latin by default
  "}}}
  " Backups (do not disturb SSD) {{{
    set nobackup      " no tilde backup files (eg. myfile.txt~)
    set nowritebackup " no backups on save
    set noswapfile    " no swap files (eg. .myfile.txt.swp)
  "}}}
  " Folding {{{
    set foldenable
    set foldcolumn=0      " no folding columns
    set foldmethod=indent
    set foldlevel=99      " all folds are open
  "}}}

" Section: Plugins options {{{1

  " CtrlP {{{2
    let g:ctrlp_match_window_bottom = 0 " Show at top of window
    let g:ctrlp_custom_ignore = '\.git$\|\.hg$\|\.svn$'
    " let g:ctrlp_cmd = 'CtrlPMixed' " MRU+buffers+files
    let g:ctrlp_working_path_mode = 'ra'
    let g:ctrlp_mruf_relative = 1 " Show mru files only in the working directory
    let g:ctrlp_use_caching = 0

    if executable('ag')
      let g:ctrlp_user_command = 'ag %s -l --nocolor -g ""'
    else
      let g:ctrlp_user_command = ['.git', 'cd %s && git ls-files . --cached --exclude-standard --others']
    endif

  " vim-surround {{{2
    let g:surround_{char2nr('#')} = "#{\r}" " ysiw# to wrap word under cursow with #{}

  " NERDTree {{{2
    let NERDTreeMinimalUI = 1        " Disable bookmarks and help labels
    let NERDTreeAutoDeleteBuffer = 1 " Delete opened buffer when file removed/renamed
    let NERDTreeQuitOnOpen = 1       " Close tree on file open
    let NERDChristmasTree = 1        " Be colorful

  " ack.vim {{{2
    let g:ackprg='ack -H --nocolor --nogroup --column --smart-case --sort-files'

  " vimux {{{2
    let g:VimuxHeight = '30'
    let g:VimuxOrientation = 'v'
    let g:VimuxPromptString = 'vimux: '

  " AutoClose {{{2
    let g:AutoCloseExpandEnterOn = ""
    let g:AutoClosePairs = {'(': ')', '{': '}', '[': ']', '"': '"', "'": "'"}

  " UndoTree {{{2
    let g:undotree_SetFocusWhenToggle = 1

  " vim-eunuch {{{2
    " :rename abbr for :Rename :)
    cabbrev rename <c-r>=getcmdpos() == 1 && getcmdtype() == ":" ? "Rename" : "rename"<CR>

  " vimerl {{{2
    let erlang_folding = 1

  " vim2hs {{{2
    let g:haskell_conceal = 0

  " neco-ghc {{{2
    " Show detailed information (type) about symbols
    let g:necoghc_enable_detailed_browse = 1

  " ghc-mod {{{2
    let g:ghcmod_ghc_options = ['-fno-warn-unused-do-bind']

  " tagbar {{{2
    let g:tagbar_compact = 1     " Omit help and empty lines
    let g:tagbar_autoshowtag = 1 " Auto open closed folds
    let g:tagbar_autofocus = 1   " Move focus to tagbar window on open
    let g:tagbar_iconchars = ['▸', '▾']

    if executable('lushtags')
        let g:tagbar_type_haskell = {
            \ 'ctagsbin' : 'lushtags',
            \ 'ctagsargs' : '--ignore-parse-error --',
            \ 'kinds' : [
                \ 'm:module:0',
                \ 'e:exports:1',
                \ 'i:imports:1',
                \ 't:declarations:0',
                \ 'd:declarations:1',
                \ 'n:declarations:1',
                \ 'f:functions:0',
                \ 'c:constructors:0'
            \ ],
            \ 'sro' : '.',
            \ 'kind2scope' : {
                \ 'd' : 'data',
                \ 'n' : 'newtype',
                \ 'c' : 'constructor',
                \ 't' : 'type'
            \ },
            \ 'scope2kind' : {
                \ 'data' : 'd',
                \ 'newtype' : 'n',
                \ 'constructor' : 'c',
                \ 'type' : 't'
            \ }
        \ }
      endif

  " gitv {{{2
    let g:Gitv_DoNotMapCtrlKey = 1

  " Splitjoin {{{2
    " Remove extra whitespace
    let g:splitjoin_normalize_whitespace = 1

    " Tabular required
    let g:splitjoin_align = 1

  " YouCompleteMe {{{2
    let g:ycm_semantic_triggers = {
      \ 'haskell' : ['.'],
      \ 'ruby' : ['.', '::'],
      \ 'erlang' : [':'],
      \ 'javascript,vim,python,scala' : ['.'],
      \ }

    let g:ycm_collect_identifiers_from_comments_and_strings = 1
    let g:ycm_complete_in_comments = 1
    let g:ycm_filetype_specific_completion_to_disable = {}
    let g:ycm_use_ultisnips_completer = 1
    let g:ycm_key_list_select_completion = ['<C-n>']
    let g:ycm_key_list_previous_completion = ['<C-p>']
    let g:ycm_key_detailed_diagnostics = ''
    let g:ycm_collect_identifiers_from_tags_files = 1
    let g:ycm_seed_identifiers_with_syntax = 1

  " UltiSnipts {{{2
    let g:UltiSnipsEditSplit="vertical"
    let g:UltiSnipsExpandTrigger="<C-j>"

  " vim-tmux-navigator {{{2
    let g:tmux_navigator_no_mappings = 1

  " easymotion {{{2
    let g:EasyMotion_do_mapping = 0
    let g:EasyMotion_smartcase = 1
    let g:EasyMotion_startofline = 0 " keep cursor colum when JK motion

" Section: Commands {{{1

  " Google search
  command! -nargs=1 Google :silent call system('open https://google.com/search?q='.<q-args>.'&')

  " Convert Ruby 1.8 hash syntax to Ruby 1.9 syntax
  " based on https://github.com/henrik/dotfiles/blob/master/vim/config/commands.vim#L20
  command! -bar -range=% NotRocket execute '<line1>,<line2>s/:\(\w\+\)\s*=>/\1:/e' . (&gdefault ? '' : 'g')

" Section: Mappings {{{1

  " Temporarily disable highlighting
  nmap <silent> <leader>n :nohlsearch<CR>

  " Search and replace word under cursor
  nmap <leader>* :%s/\<<C-r><C-w>\>//<Left>

  " Rails.vim alternate and related
  nmap <leader>ra :A<CR>
  nmap <leader>rr :R<CR>
  " Same in vsplit
  nmap <leader>RA :AV<CR>
  nmap <leader>RR :RV<CR>

  " Prompt for jump to migration (then <cr> for last migration, 0<cr> for seeds)
  nmap <leader>rm :Rmigration<space>
  " Prompt for rails generate
  nmap <leader>rg :Rgenerate<space>
  " Prompt for rails extract (partial, concern, helper)
  map <leader>re :Rextract<space>

  " Highlight current word without moving the cursor
  nmap <silent> <leader>h :setl hls<CR>:let @/="\\<<C-r><C-w>\\>"<CR>

  " Trim spaces and save current buffer
  map <silent> <leader>w :call utils#trimSpaces()<CR>:w!<CR>

  " Save current buffer, trim spaces and exit
  map <leader>q <leader>w :q!<CR>

  nnoremap <leader>gb :Gblame<CR>
  nnoremap <leader>gs :Gstatus<CR>
  nnoremap <leader>gd :Gdiff<CR>
  nnoremap <leader>gw :Gwrite<CR>
  nnoremap <leader>gv :Gitv! --all<cr>
  vnoremap <leader>gv :Gitv! --all<cr>

  " Toggle nerdtree
  nmap <silent> <leader>p :NERDTreeToggle<CR>
  " Find current file in tree
  nmap <silent> <leader>P :NERDTreeFind<CR>

  " Buffer search
  nmap <silent> <leader>b :CtrlPBuffer<CR>

  " Search for tags
  nmap <silent> <leader>@ :CtrlPTag<CR>

  " Tmux (vimux) mappings
  " [l]ast
  nmap <leader>vl :VimuxRunLastCommand<CR>
  " [q]uit
  nmap <leader>vq :VimuxCloseRunner<CR>
  " [i]nspect - enter scroll (copy) mode at opened pane
  nmap <leader>vi :VimuxInspectRunner<CR>
  " [s]top
  nmap <leader>vs :VimuxInterruptRunner<CR>
  " [c]ommand
  nmap <leader>vc :VimuxPromptCommand<CR>

  " " CtrlP jump to
  " map <leader>jm :CtrlP app/models<CR>
  " map <leader>jc :CtrlP app/controllers<CR>
  " map <leader>jv :CtrlP app/views<CR>
  " map <leader>jh :CtrlP app/helpers<CR>
  " map <leader>jl :CtrlP lib<CR>
  " map <leader>js :CtrlP spec<CR>
  " map <leader>jf :CtrlP spec/factories<CR>
  " map <leader>jd :CtrlP db<CR>
  " map <leader>jC :CtrlP config<CR>

  " Switch between last two buffers
  nnoremap <leader><leader> <c-^>

  " Edit file in current dir ([e]dit in [w]indow, [s]plit, [v]split)
  cnoremap %% <C-R>=expand('%:p:h').'/'<cr>
  map <leader>ew :edit %%
  map <leader>ev :vsplit %%
  map <leader>es :split %%

  " Prompt for ack search
  nnoremap <leader>f :Ack!<space>"<left>"
  " Search for word under cursor
  nnoremap <leader>F *<C-O>:AckFromSearch!<cr>

  " Toggle undotree
  nnoremap <silent><leader>u :UndotreeToggle<CR>

  " Markdown preview
  nnoremap <leader>m :!open -a Marked.app '%:p'<cr><cr>

  " Insert new line
  map <leader>o o<ESC>
  map <leader>O O<ESC>

  nnoremap <silent><leader>yf :let @+ = expand("%")<cr>

  " Select last inserted/yanked text
  nmap gV `[v`]

  " Intelligently close a window
  nnoremap <silent> Q :call utils#closeWindowOrKillBuffer()<CR>

  " Toggle relative and absolute line numbers
  nnoremap <C-n> :set relativenumber!<CR>

  " Yank from cursor to end of line
  nnoremap Y y$

  " Paste toggle
  set pastetoggle=<F5>

  nnoremap <silent> <F8> :TagbarToggle<CR>

  " Scroll viewport by 3 lines
  nnoremap <C-e> 3<C-e>
  nnoremap <C-y> 3<C-y>

  " H to start of line, L to end of line
  noremap H ^
  noremap L $

  " ruby evalution
  nmap <buffer> <F6> <Plug>(xmpfilter-mark)
  xmap <buffer> <F6> <Plug>(xmpfilter-mark)
  nmap <buffer> <F7> <Plug>(xmpfilter-run)
  xmap <buffer> <F7> <Plug>(xmpfilter-run)

  " C-f for switch keyboard layout
  inoremap <C-f> <C-^>
  cnoremap <C-f> <C-^>

  " Use the text that has already been typed as the prefix for searching
  " through commands
  cnoremap <C-n> <t_kd>
  cnoremap <C-p> <t_ku>

  " C-a & C-e should work as expected at command-line mode
  cnoremap <C-a> <Home>
  cnoremap <C-e> <End>

  " Filetype switching
  nnoremap _rb = :set filetype=ruby<CR>
  nnoremap _jn = :set filetype=json<CR>
  nnoremap _hs = :set filetype=haskell<CR>
  nnoremap _sh = :set filetype=sh<CR>

  " Search file in current dir
  nmap \ :CtrlP %%<CR>

  " Ignore linewraps on jk
  nnoremap j gj
  nnoremap k gk
  vnoremap j gj
  vnoremap k gk

  " Use Tab instead of % to switch using matchit
  nmap <tab> %
  vmap <tab> %

  " Easymotion mappings
  nmap s <Plug>(easymotion-s2)
  vmap s <Plug>(easymotion-s2)
  omap gs <Plug>(easymotion-s2)
  map <Leader>j <Plug>(easymotion-j)
  map <Leader>k <Plug>(easymotion-k)

  omap t <Plug>(easymotion-tl)
  omap f <Plug>(easymotion-fl)

  omap T <Plug>(easymotion-Tl)
  omap F <Plug>(easymotion-Fl)


  nnoremap <silent> <c-h> :TmuxNavigateLeft<cr>
  nnoremap <silent> <c-j> :TmuxNavigateDown<cr>
  nnoremap <silent> <c-k> :TmuxNavigateUp<cr>
  nnoremap <silent> <c-l> :TmuxNavigateRight<cr>

  " :)
  inoremap  <Up>     <NOP>
  inoremap  <Down>   <NOP>
  inoremap  <Left>   <NOP>
  inoremap  <Right>  <NOP>
  noremap   <Up>     <NOP>
  noremap   <Down>   <NOP>
  noremap   <Left>   <NOP>
  noremap   <Right>  <NOP>

" Section: Autocommands {{{1

  augroup RestorePosition " {{{
    autocmd!
    autocmd BufWinEnter *
      \ if &filetype !~ 'commit\c' && line("'\"") > 0 && line("'\"") <= line("$") |
      \     execute 'normal! g`"zvzz' |
      \ endif
  augroup END "}}}
  augroup FtOptions "{{{
    autocmd!

    autocmd Filetype gitcommit setlocal textwidth=72
    autocmd Filetype git,gitcommit setlocal foldenable foldmethod=syntax

    autocmd FileType vim setlocal keywordprg=:help foldenable foldmethod=marker foldlevel=0

    autocmd BufWritePost *.hs GhcModCheckAndLintAsync
  augroup END "}}}
  augroup Stuff "{{{
    autocmd!

    " Resize splits when the window is resized
    autocmd VimResized * :wincmd =
  augroup END "}}}

" Section: UI {{{1

  set background=dark
  colorscheme hybrid

  " Enable syntax higlighter
  syntax on

  " Highlight VCS conflict markers (thanks, @sjl)
  match ErrorMsg '^\(<\|=\|>\)\{7\}\([^=].\+\)\?$'
